## 所有内置的代码命令语句

**⚠️这里仅收录了ZBasic插件自带的所有命令语句，部分插件可能会载入更多的扩展语句，如果想要使用，请查看其的教程！**

---
> 设置 <变量名> 为 <表达式>

- 为变量赋值。 表达式可以是 `1.2` 或者 `"你好"` 这样的常量， 也可以是算数表达式。
如果变量已被赋值，它之后再被赋值，变量类型不能发送改变。
- 无法直接使用 变量名=表达式 的方法来为变量赋值。因为在ZBasic中，等于号是**判等符号**而不是赋值符号。
- 示例：
```
设置 随机一个数 为 随机整数 1, 2
设置 显示的消息 为 "你抽到的幸运数字是：" + 随机一个数 + "， 恭喜你！"
# 在 ZBasic 语法里，数值可以和字符串相加，结果为字符串
```

---

> 输出 <表达式>

- 在控制台输出表达式内容。
可以使用分号 ";" 来分隔多个变量，输出的时候会直接拼接多个变量。
示例（运行结果为 `你好38你是个好人`，仅取某一次的随机数值） ：
```
设置 a 为 "你"
设置 b 为 "好"
输出 a; b; 1+2; 随机整数 5, 8; "你是个好人"
```
---

> @导入变量 <变量名> <变量类型>

- 向外部索取变量数据。在被执行的时候会索取外部的变量信息到本地。
变量类型： 数值 / 字符串（目前为止）
例如，该脚本如果**监听玩家发言**（../脚本文件/.../玩家发言.txt），就可以使用如下方法导入`玩家名`和`消息`这两个变量：
```
@导入变量 玩家名 字符串
@导入变量 消息 字符串

输出 玩家名; 消息
```
---

> 如果 <条件:表达式> 那么
...（代码块）
又或者 <条件:表达式> 那么
...
否则
...（代码块）
结束如果

- 判定一个表达式成不成立。 成立则执行“如果”以下的代码；如果存在“又或者”的代码，那么在上方代码判断结果不成立时，就会再判断一遍“又或者”的条件，成功则执行；都不成立则执行“否则”以下的代码。
示例：
```
如果 玩家名="Herobrine" 那么
    输出 "Him来了!"
又或者 玩家名="303" 那么
    输出 "我感觉背后一凉..."
又或者 玩家名="null" 那么
    输出 "我感觉我要虚化了..."
否则
    输出 "嗯，很正常。"
结束如果
```
- ⚠️ 和大多数编程语言一样，一般来说，在“如果”代码块里的变量，如果没有在所有的子代码块里都赋值了（而且变量类型相同）的话，这个变量是没有办法在外层代码使用的，会导致“变量可能没有完全绑定”报错。
如：
```
设置 c 为 "Hi"
如果 1=2 那么
    设置 a 为 1
    设置 b 为 3
否则
    设置 a 为 2
    设置 c 为 "World"

输出 a
# 输出 a 是没有问题的，因为a在 “如果” 和 “否则” 代码块都被赋值过

输出 c
# 输出 c 是没有问题的，因为c在 “如果” 语句之前就已经被赋值过了

输出 b
# 会引发报错，因为在“否则”代码块中没有为b赋值， 而且在先前也没有被赋值过
```
---

> 循环执行直到 <条件:表达式>
...（代码块）
结束循环

- 循环执行下方的代码， 直到条件成立。
示例：
```
设置 a 为 0
循环执行直到 a>5
    设置 a 为 a + 1
    输出 a
结束循环
```
- ⚠️ 在循环代码块内的变量如果没有在外层定义，而是只在循环内定义的话，将不能在外层代码使用里面的变量。

---

> 执行 <表达式:字符串>

- 执行 Minecraft 指令。
示例：
```
执行 "/give " + 玩家名 + " diamond"
```
---

> 聊天栏显示 <目标 表达式:字符串> ; <内容 表达式:字符串>

- 向指定目标的聊天栏显示内容。
目标可以是玩家名或者目标选择器。
示例：
```
聊天栏显示 玩家名; "亲爱的" + 玩家名 + "，你吃了吗？"
```

---

> 忽略空变量 <变量名>

- 由于某些表达式返回的结果为 `可空变量[...]`， 所以需要排除其为空的可能。
例如：`获取玩家计分板分数` 函数，由于计分板可能不存在或者玩家没有分数记录等情况，会使得其无法正常获取到玩家的分数，此时就会返回一个空变量。空变量不能用于正常运算。
- 而 `忽略空变量` 命令的作用是：如果变量可能为空， 则忽略为空的可能（让编译器认为这不是可能为空的变量）。运行时，**当该变量为空的时候直接退出脚本的执行。**
通常放在用 如果 为空变量 <变量名> 则 ... 代码块判断了是否为空变量之后使用。
示例：
```
设置 钱数 为 获取玩家计分板分数 玩家名, "money"
# 要考虑到获取不到分数的诸多情况，因此该变量可能为空
如果 为空变量 钱数 那么
    # 获取不到 该玩家 的 money 计分板值
    设置 钱数1 为 0
否则
    忽略空变量 钱数
    # 经过了上面的排除空变量
    # 此时 "钱数" 这个变量一定不是空变量
    设置 钱数1 为 钱数
结束如果
输出 钱数
```

---

